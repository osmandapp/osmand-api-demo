apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    dexOptions {
        jumboMode true
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    // This is from OsmAndCore_android.aar - for some reason it's not inherited
    aaptOptions {
        // Don't compress any embedded resources
        noCompress "qz", "png"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            jni.srcDirs = []
            jniLibs.srcDirs = ["libs"]
            java.srcDirs = ["src"]
            renderscript.srcDirs = ["src"]
            resources.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }
    }

    flavorDimensions "abi"

    productFlavors {
        x86 {
            dimension "abi"
            ndk {
                abiFilter "x86"
            }
        }
        arm64 {
            dimension "abi"
            ndk {
                abiFilter 'arm64-v8a'
            }
        }
        armv7 {
            dimension "abi"
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        fat {
            dimension "abi"
			ndk {
				abiFilters 'arm64-v8a', 'x86', 'armeabi-v7a'
			}
        }
    }

    signingConfigs {
        publishing {
            storeFile file("/var/lib/jenkins/osmand_key")
            storePassword System.getenv("OSMAND_APK_PASSWORD")
            keyAlias "osmand"
            keyPassword System.getenv("OSMAND_APK_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig android.signingConfigs.debug
        }
        nativeDebug {
            signingConfig android.signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.publishing
        }
    }
}

task collectMiscResources(type: Copy) {
    into "src/net/osmand/osm"
    from("../../../resources/poi") {
        include "poi_types.xml"
    }
}

task collectRegionsInfoResources(type: Copy) {
    from "../../../resources/countries-info"
    into "src/net/osmand/map"
    include "regions.ocbf"
}

task collectRoutingResources(type: Sync) {
    from "../../../resources/routing"
    into "src/net/osmand/router"
    include "routing.xml"
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << [collectMiscResources, collectRegionsInfoResources, collectRoutingResources]
}

dependencies {
    implementation "net.osmand:OsmAnd-java:1.0:android@jar"

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'commons-logging:commons-logging-api:1.1'
    implementation 'com.moparisthebest:junidecode:0.1.1'

    implementation fileTree(include:  ['gnu-trove-osmand.jar', 'icu4j-49_1_patched.jar'], dir: 'libs')

    implementation "net.osmand:OsmAndCore_android:0.1-SNAPSHOT@aar"
    debugImplementation "net.osmand:OsmAndCore_androidNativeRelease:0.1-SNAPSHOT@aar"
    nativeDebugImplementation "net.osmand:OsmAndCore_androidNativeDebug:0.1-SNAPSHOT@aar"
    releaseImplementation "net.osmand:OsmAndCore_androidNativeRelease:0.1-SNAPSHOT@aar"

    implementation files('libs/QtAndroid.jar')
    implementation files('libs/QtAndroidBearer.jar')

    // Local build
    //implementation 'com.vividsolutions:jts-core:1.14.0'
    //implementation(name: 'OsmAndCore_android-release', ext: 'aar')
    //implementation(name: 'OsmAndCore_androidNativeRelease-release', ext: 'aar')
}
