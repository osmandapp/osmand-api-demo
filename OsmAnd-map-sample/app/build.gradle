apply plugin: 'com.android.application'

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"

	defaultConfig {
		applicationId "net.osmand.library.sample"
		minSdkVersion 21
		targetSdkVersion 29
		versionCode 1
		versionName "1.0"
		multiDexEnabled true

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	lintOptions {
		lintConfig file("lint.xml")
		abortOnError false
		warningsAsErrors false
	}

	flavorDimensions "coreversion", "abi"
	productFlavors {
		// ABI
		armv7 {
			dimension "abi"
			ndk {
				abiFilter 'armeabi-v7a'
			}
		}
		arm64 {
			dimension "abi"
			ndk {
				abiFilter 'arm64-v8a'
			}
		}
		x86 {
			dimension "abi"
			ndk {
				abiFilters 'x86', 'x86_64'
			}
		}
		armonly {
			dimension "abi"
			ndk {
				abiFilters 'arm64-v8a', 'armeabi-v7a'
			}
		}
		fat {
			dimension "abi"
			ndk {
				abiFilters 'arm64-v8a', 'x86', 'x86_64', 'armeabi-v7a'
			}
		}

		// CoreVersion
		// Build that doesn't include 3D OpenGL
		legacy {
			dimension "coreversion"
		}
	}

	signingConfigs {
		publishing {
			storeFile file("/var/lib/jenkins/osmand_key")
			storePassword System.getenv("OSMAND_APK_PASSWORD")
			keyAlias "osmand"
			keyPassword System.getenv("OSMAND_APK_PASSWORD")
		}
	}

	buildTypes {
		debug {
			signingConfig android.signingConfigs.debug
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.publishing
		}
	}
}

dependencies {
	implementation "net.osmand:OsmAnd-java:1.0:android@jar"
	debugImplementation "net.osmand:OsmAnd:0.1-SNAPSHOT:debug@aar"
	releaseImplementation "net.osmand:OsmAnd:0.1-SNAPSHOT:release@aar"

	implementation 'androidx.multidex:multidex:2.0.1'
	implementation 'androidx.gridlayout:gridlayout:1.0.0'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'com.google.android.material:material:1.4.0'
	implementation 'androidx.browser:browser:1.0.0'
	implementation 'androidx.preference:preference:1.1.0'

	implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
	implementation 'commons-codec:commons-codec:1.11'
	implementation 'org.apache.commons:commons-compress:1.17'
	implementation 'com.moparisthebest:junidecode:0.1.1'
	implementation 'org.immutables:gson:2.5.0'
	implementation 'com.vividsolutions:jts-core:1.14.0'
	implementation 'com.google.openlocationcode:openlocationcode:1.0.4'
	implementation 'com.android.billingclient:billing:3.0.2'

	implementation 'com.squareup.picasso:picasso:2.71828'
	implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
	// JS core
	implementation group: 'org.mozilla', name: 'rhino', version: '1.7.9'

	implementation ("com.getkeepsafe.taptargetview:taptargetview:1.13.2"){
		exclude group: 'com.android.support'
	}
	implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'
	implementation ("com.github.HITGIF:TextFieldBoxes:1.4.5"){
		exclude group: 'com.android.support'
	}
	implementation('com.github.scribejava:scribejava-apis:7.1.1'){
		exclude group: "com.fasterxml.jackson.core"
	}
	implementation 'com.jaredrummler:colorpicker:1.1.0'
	implementation 'com.google.android.gms:play-services-location:19.0.1'

	testImplementation 'junit:junit:4.+'
	androidTestImplementation 'androidx.test.ext:junit:1.1.1'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
